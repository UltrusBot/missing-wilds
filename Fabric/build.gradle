
plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    alias libs.plugins.publishing
}

archivesBaseName = "${mod_name}-fabric"
version = "${version}+${minecraft_version}"
repositories {
    maven{ url "https://jitpack.io"}
    maven{ url "https://ueaj.dev/maven"}
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroup("dev.emi")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft(libs.minecraft)
    mappings(loom.officialMojangMappings())
    modImplementation(libs.fabric.loader)
    modImplementation(libs.fabric.api)
    implementation(project(":Common"))
    modImplementation(libs.arrp)
    modImplementation(libs.templates)

    modRuntimeOnly(libs.emi)
}

loom {
    accessWidenerPath = file("src/main/resources/missingwilds.accesswidener")
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
        datagenCommon {
            client()
            setConfigName("Fabric Datagen Common")
            vmArg("-Dmw.common=true")
            vmArg("-Dfabric-api.datagen")
            vmArg("-Dfabric-api.datagen.output-dir=${file("../Common/src/generated/resources")}")
            vmArg("-Dfabric-api.datagen.modid=${mod_id}")
            runDir = "build/datagen"
        }
    }
}

publishMods {
    displayName = "Missing Wilds Fabric ${project.version}"
    file = remapJar.archiveFile
    changelog = file("$rootDir/CHANGELOG.md").text
    version = "v${project.version}-fabric"
    type = STABLE
    modLoaders.addAll("fabric")

    curseforge {
        projectId = "622590"
        minecraftVersions.addAll("1.20.1")
        accessToken = providers.environmentVariable("CF_API_KEY")
        requires {
            slug = "fabric-api"
        }
        optional {
            slug = "arrp"
        }
    }
    modrinth {
        projectId = "Nw0MStSd"
        minecraftVersions.addAll("1.20.1")
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        requires {
            slug = "fabric-api"
        }
        optional {
            "arrp"
        }
    }
    github {
        repository = "UltrusBot/missing-wilds"
        commitish = "1.20"
        accessToken = providers.environmentVariable("GH_TOKEN")
    }

    dryRun = false
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    exclude '.cache'
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
                    artifactId project.archivesBaseName
                    version project.version
                    from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}