import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id "org.quiltmc.loom"  version "0.12.+"
    id "maven-publish"
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.1.15'
}
apply from: "https://raw.githubusercontent.com/UltrusBot/GradleScripts/${project.upload_script_version}/release.gradle"

archivesBaseName = "${mod_name}-quilt-${minecraft_version}"

repositories {
    maven {
        name = "Quilt"
        url = "https://maven.quiltmc.org/repository/release"
        content {
            includeGroupByRegex('org\\.quiltmc.*')
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroup("dev.emi")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {

    minecraft(libs.minecraft)
    mappings(loom.officialMojangMappings())
    modImplementation(libs.quilt.loader)

    modImplementation(libs.qfapi)
//    modImplementation(libs.emi) //TODO 1.20
//    modImplementation(libs.tablesaw)

    modImplementation(libs.defaultresources.quilt) {
        transitive = false
    }
    include(libs.defaultresources.quilt)

    implementation(project(":Common"))
}
loom {
    accessWidenerPath = file("src/main/resources/missingwilds.accesswidener")
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
}

var mod = createModReleaseMetadata(
        "Missing Wilds v1.2.0+1.19.4 Beta 2 - Quilt",
        file("$rootDir/CHANGELOG.md"),
        ["1.19.4"],
        ["quilt"],
        "remapJar",
        ["sourcesJar", "javadocJar"],
        "v${project.version}+${project.minecraft_version}-quilt",
        project.testRelease.toBoolean(),
        project.releaseType
)
modrinthInfo(mod, "missing-wilds", ["qsl"])
githubInfo(mod, "UltrusBot", "missing-wilds", "1.19.4")
curseforgeInfo(mod, TaskPublishCurseForge, "622590", [], ["qsl"])


processResources {

    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("quilt.mod.json") {
        expand "version": project.version
    }
    from(rootProject.rootDir.absolutePath + "/ModCompat") {
        into "defaultresources"
    }

    exclude '.cache'
}

tasks.withType(JavaCompile) {

    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}