plugins {
    id "org.quiltmc.loom"  version "0.12.+"
    id "maven-publish"
    alias libs.plugins.publishing
}

archivesBaseName = "${mod_name}-quilt"
version = "${version}+${minecraft_version}"

repositories {
    maven {
        name = "Quilt"
        url = "https://maven.quiltmc.org/repository/release"
        content {
            includeGroupByRegex('org\\.quiltmc.*')
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroup("dev.emi")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {

    minecraft(libs.minecraft)
    mappings(loom.officialMojangMappings())
    modImplementation(libs.quilt.loader)

    modImplementation(libs.qfapi)
    modRuntimeOnly(libs.emi)
    modImplementation(libs.tablesaw)

    modImplementation(libs.defaultresources.quilt) {
        transitive = false
    }
    include(libs.defaultresources.quilt)
    modImplementation(libs.templates)

    implementation(project(":Common"))
}
loom {
    accessWidenerPath = file("src/main/resources/missingwilds.accesswidener")
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
}

publishMods {
    displayName = "Missing Wilds Quilt ${project.version}"
    file = remapJar.archiveFile
    changelog = file("$rootDir/CHANGELOG.md").text
    version = "v${project.version}-quilt"
    type = STABLE
    modLoaders.addAll("quilt")

    curseforge {
        projectId = "622590"
        minecraftVersions.addAll("1.20.1")
        accessToken = providers.environmentVariable("CF_API_KEY")
        requires {
            slug = "qsl"
        }
    }
    modrinth {
        projectId = "Nw0MStSd"
        minecraftVersions.addAll("1.20.1")
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        requires {
            slug = "qsl"
        }
    }
    github {
        repository = "UltrusBot/missing-wilds"
        commitish = "1.20"
        accessToken = providers.environmentVariable("GH_TOKEN")
    }

    dryRun = false
}


processResources {

    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("quilt.mod.json") {
        expand "version": project.version
    }

    exclude '.cache'
}

tasks.withType(JavaCompile) {

    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}